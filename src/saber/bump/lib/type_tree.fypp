#:include 'instrumentation.fypp'
!----------------------------------------------------------------------
! Module: type_tree
!> Tree derived type
! Author: Benjamin Menetrier
! Licensing: this code is distributed under the CeCILL-C license
! Copyright 2015-... UCAR, CERFACS, METEO-FRANCE and IRIT
!----------------------------------------------------------------------
module type_tree

use atlas_module, only: atlas_geometry,atlas_indexkdtree
use iso_c_binding, only: c_ptr
use tools_const, only: zero,half,two,pi,rad2deg
use tools_func, only: lonlat2xyz,sphere_dist
use tools_kinds, only: kind_real
use tools_qsort, only: qsort
use tools_repro, only: repro_ops,sup,indist
use type_mpl, only: mpl_type
@:use_probe()

implicit none

real(kind_real),parameter :: nn_inc = 1.5_kind_real !< Increase factor for the nearest neighbors numbers search

! Tree derived type
type tree_type
    integer :: n                              !< Data size
    integer :: neff                           !< Effective tree size
    logical,allocatable :: mask(:)            !< Mask
    integer,allocatable :: from_eff(:)        !< Effective index conversion
    real(kind_real),allocatable :: lon(:)     !< Longitudes
    real(kind_real),allocatable :: lat(:)     !< Latitudes
    type(atlas_indexkdtree) :: kd             !< KDTree from ATLAS
contains
    procedure :: alloc => tree_alloc
    procedure :: init => tree_init
    procedure :: dealloc => tree_dealloc
    procedure :: find_nearest_neighbors => tree_find_nearest_neighbors
    procedure :: count_nearest_neighbors => tree_count_nearest_neighbors
end type tree_type

private
public :: tree_type

contains

!----------------------------------------------------------------------
! Subroutine: tree_alloc
!> Allocation
!----------------------------------------------------------------------
subroutine tree_alloc(tree,mpl,n,mask)

implicit none

! Passed variables
class(tree_type),intent(inout) :: tree !< Tree
type(mpl_type),intent(inout) :: mpl    !< MPI data
integer,intent(in) :: n                !< Number of points
logical,intent(in),optional :: mask(n) !< Mask

! Set name
@:set_name(tree_alloc)

! Probe in
@:probe_in()

! Allocation
tree%n = n
allocate(tree%mask(n))

! Mask
if (present(mask)) then
   tree%mask = mask
else
   tree%mask = .true.
end if

! Effective tree size
tree%neff = count(tree%mask)

! Allocation
allocate(tree%lon(tree%neff))
allocate(tree%lat(tree%neff))
allocate(tree%from_eff(tree%neff))

! Probe out
@:probe_out()

end subroutine tree_alloc

!----------------------------------------------------------------------
! Subroutine: tree_init
!> Initialization
!----------------------------------------------------------------------
subroutine tree_init(tree,lon,lat)

implicit none

! Passed variables
class(tree_type),intent(inout) :: tree    !< Tree
real(kind_real),intent(in) :: lon(tree%n) !< Points longitudes [in radians]
real(kind_real),intent(in) :: lat(tree%n) !< Points latitudes [in radians]

! Local variable
integer :: i,ieff
real(kind_real) :: lon_deg(tree%neff),lat_deg(tree%neff)
type(atlas_geometry) :: ageometry

! Set name
@:set_name(tree_init)

! Probe in
@:probe_in()

! Loop over points
ieff = 0
do i=1,tree%n
   if (tree%mask(i)) then
      ! Increment index
      ieff = ieff+1

      ! Conversion
      tree%from_eff(ieff) = i

      ! Copy lon/lat
      tree%lon(ieff) = lon(i)
      tree%lat(ieff) = lat(i)
   end if
end do

! Create geometry
ageometry = atlas_geometry('UnitSphere')

! Create KDTree
lon_deg = tree%lon*rad2deg
lat_deg = tree%lat*rad2deg
tree%kd = atlas_indexkdtree(ageometry)
call tree%kd%reserve(tree%neff)
call tree%kd%build(tree%neff,lon_deg,lat_deg)

! Probe out
@:probe_out()

end subroutine tree_init

!----------------------------------------------------------------------
! Subroutine: tree_dealloc
!> Release memory
!----------------------------------------------------------------------
subroutine tree_dealloc(tree)

implicit none

! Passed variables
class(tree_type),intent(inout) :: tree !< Tree

! Set name
@:set_name(tree_dealloc)

! Probe in
@:probe_in()

if (allocated(tree%from_eff)) then
   ! Release memory
   deallocate(tree%mask)
   deallocate(tree%from_eff)
   deallocate(tree%lon)
   deallocate(tree%lat)

   ! Destroy KDTree
   call tree%kd%final()
end if

! Probe out
@:probe_out()

end subroutine tree_dealloc

!----------------------------------------------------------------------
! Subroutine: tree_find_nearest_neighbors
!> Find nearest neighbors using a KDTree
!----------------------------------------------------------------------
subroutine tree_find_nearest_neighbors(tree,lon,lat,nn,nn_index,nn_dist)

implicit none

! Passed variables
class(tree_type),intent(in) :: tree                 !< Tree
real(kind_real),intent(in) :: lon                   !< Point longitude [in radians]
real(kind_real),intent(in) :: lat                   !< Point latitude [in radians]
integer,intent(in) :: nn                            !< Number of nearest neighbors to find
integer,intent(out) :: nn_index(nn)                 !< Nearest neighbors index
real(kind_real),intent(out),optional :: nn_dist(nn) !< Nearest neighbors distance

! Local variables
integer :: nn_tmp,i,j,nid
integer,allocatable :: order(:),nn_index_tmp(:)
real(kind_real) :: dist_ref,dist_last
real(kind_real),allocatable :: lontmp(:),lattmp(:),nn_dist_tmp(:)
logical :: separate

! Set name
@:set_name(tree_find_nearest_neighbors)

! Probe in
@:probe_in()

if (nn>0) then
   ! Initialization
   separate = .false.
   nn_tmp = min(nn+1,tree%neff)

   do while (.not.separate)
      ! Allocation
      allocate(nn_index_tmp(nn_tmp))

      ! Find neighbors
      call tree%kd%closestPoints(lon*rad2deg,lat*rad2deg,nn_tmp,nn_index_tmp)

      ! Check distance between reference and last nearest neighbors
      call sphere_dist(lon,lat,tree%lon(nn_index_tmp(nn)),tree%lat(nn_index_tmp(nn)),dist_ref)
      call sphere_dist(lon,lat,tree%lon(nn_index_tmp(nn_tmp)),tree%lat(nn_index_tmp(nn_tmp)),dist_last)
      if (repro_ops.and.indist(dist_last,dist_ref).and.(nn_tmp<tree%neff)) then
         ! Last neighbor is at the same distance as the reference neighbor, increase number of neighbors
         nn_tmp = min(int(nn_inc*real(nn_tmp,kind_real)),tree%neff)

         ! Release memory
         deallocate(nn_index_tmp)
      else
         ! Last neighbor is significantly further away (or all points are used)
         separate = .true.
      end if
   end do

   ! Allocation
   allocate(nn_dist_tmp(nn_tmp))

   ! Compute distance
   do i=1,nn_tmp
      call sphere_dist(lon,lat,tree%lon(nn_index_tmp(i)),tree%lat(nn_index_tmp(i)),nn_dist_tmp(i))
   end do

   if (repro_ops) then
      ! Reorder neighbors based on their lon/lat
      i = 1
      do while (i<nn_tmp)
         ! Count indistinguishable neighbors
         nid = 1
         do j=i+1,nn_tmp
            if (indist(nn_dist_tmp(i),nn_dist_tmp(j))) nid = nid+1
         end do

         if (nid>1) then
            ! Allocation
            allocate(lontmp(nid))
            allocate(lattmp(nid))
            allocate(order(nid))

            ! Sort lon/lat
            do j=1,nid
               lontmp(j) = tree%lon(nn_index_tmp(i+j-1))
               lattmp(j) = tree%lat(nn_index_tmp(i+j-1))
            end do
            call qsort(nid,lontmp,lattmp,order)

            ! Reorder
            order = i+order-1
            nn_index_tmp(i:i+nid-1) = nn_index_tmp(order)
            nn_dist_tmp(i:i+nid-1) = nn_dist_tmp(order)

            ! Release memory
            deallocate(lontmp)
            deallocate(lattmp)
            deallocate(order)
         end if

         ! Update
         i = i+nid
      end do
   end if

   ! Transform indices
   nn_index_tmp = tree%from_eff(nn_index_tmp)

   ! Copy nn_index
   nn_index = nn_index_tmp(1:nn)

   ! Copy nn_dist if required
   if (present(nn_dist)) nn_dist = nn_dist_tmp(1:nn)

   ! Release memory
   deallocate(nn_index_tmp)
   deallocate(nn_dist_tmp)
end if

! Probe out
@:probe_out()

end subroutine tree_find_nearest_neighbors

!----------------------------------------------------------------------
! Subroutine: tree_count_nearest_neighbors
!> Count nearest neighbors using a tree
!----------------------------------------------------------------------
subroutine tree_count_nearest_neighbors(tree,lon,lat,sr,nn)

implicit none

! Passed variables
class(tree_type),intent(in) :: tree !< Tree
real(kind_real),intent(in) :: lon   !< Point longitude [in radians]
real(kind_real),intent(in) :: lat   !< Point latitude [in radians]
real(kind_real),intent(in) :: sr    !< Spherical radius [in radians]
integer,intent(out) :: nn           !< Number of nearest neighbors found

! Local variable
real(kind_real) :: ch

! Set name
@:set_name(tree_count_nearest_neighbors)

! Probe in
@:probe_in()

! Spherical radius to chord
ch = two*sin(half*min(sr,pi))

! Count nearest neighbors
call tree%kd%closestPointsWithinRadius(lon*rad2deg,lat*rad2deg,ch,nn)

! Probe out
@:probe_out()

end subroutine tree_count_nearest_neighbors

end module type_tree
