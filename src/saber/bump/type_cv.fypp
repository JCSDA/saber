#:include 'instrumentation.fypp'
!----------------------------------------------------------------------
! Module: type_cv
!> Control vector derived type
! Author: Benjamin Menetrier
! Licensing: this code is distributed under the CeCILL-C license
! Copyright 2015-... UCAR, CERFACS, METEO-FRANCE and IRIT
!----------------------------------------------------------------------
module type_cv

use atlas_module, only: atlas_field
use tools_kinds, only: kind_real
use type_cv_blk, only: cv_blk_type
@:use_probe()

implicit none

! Control vector derived type
type cv_type
   integer :: n                            !< Total control variable size
   integer :: nb                           !< Number of blocks
   type(cv_blk_type),allocatable :: blk(:) !< Control variable blocks
   integer,allocatable :: b_to_g(:)        !< CV block to NICAS group
contains
   procedure :: pack => cv_pack
   procedure :: unpack => cv_unpack
end type cv_type

private
public :: cv_type

contains

!----------------------------------------------------------------------
! Subroutine: cv_pack
!> Pack
!----------------------------------------------------------------------
subroutine cv_pack(cv,acv,offset)

! Passed variables
class(cv_type),intent(in) :: cv        !< Control variable
type(atlas_field),intent(inout) :: acv !< ATLAS field control vector
integer,intent(in) :: offset           !< Control vector offset

! Local variable
integer :: ib,icmp,offset_tmp
real(kind_real),pointer :: ptr(:)

! Set name
@:set_name(cv_pack)

! Probe in
@:probe_in()

! Initialization
offset_tmp = offset
call acv%data(ptr)

do ib=1,cv%nb
   do icmp=1,cv%blk(ib)%ncmp
      if (cv%blk(ib)%cmp(icmp)%n>0) then
         ! Pack control variable
         ptr(offset_tmp+1:offset_tmp+cv%blk(ib)%cmp(icmp)%n) = cv%blk(ib)%cmp(icmp)%alpha

         ! Update
         offset_tmp = offset_tmp+cv%blk(ib)%cmp(icmp)%n
      end if
   end do
end do

! Probe out
@:probe_out()

end subroutine cv_pack

!----------------------------------------------------------------------
! Subroutine: cv_unpack
!> Unpack
!----------------------------------------------------------------------
subroutine cv_unpack(cv,acv,offset)

! Passed variables
class(cv_type),intent(inout) :: cv  !< Control variable
type(atlas_field),intent(in) :: acv !< ATLAS field control vector
integer,intent(in) :: offset        !< Control vector offset

! Local variable
integer :: ib,icmp,offset_tmp
real(kind_real),pointer :: ptr(:)

! Set name
@:set_name(cv_unpack)

! Probe in
@:probe_in()

! Initialization
offset_tmp = offset
call acv%data(ptr)

do ib=1,cv%nb
   do icmp=1,cv%blk(ib)%ncmp
      if (cv%blk(ib)%cmp(icmp)%n>0) then
         ! Unpack control variable
         cv%blk(ib)%cmp(icmp)%alpha = ptr(offset_tmp+1:offset_tmp+cv%blk(ib)%cmp(icmp)%n)

         ! Update
         offset_tmp = offset_tmp+cv%blk(ib)%cmp(icmp)%n
      end if
   end do
end do

! Probe out
@:probe_out()

end subroutine cv_unpack

end module type_cv
