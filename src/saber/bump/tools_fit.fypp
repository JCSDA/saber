#:include '../instrumentation.fypp'
!----------------------------------------------------------------------
! Module: tools_fit
!> Fit-related tools
! Author: Benjamin Menetrier
! Licensing: this code is distributed under the CeCILL-C license
! Copyright 2015-... UCAR, CERFACS, METEO-FRANCE and IRIT
!----------------------------------------------------------------------
module tools_fit

use tools_const, only: zero,quarter,half,one
use tools_gc99, only: nnld,dnld,nnlr,dnlr,fit_func,ver_minval,ver_minarg
use tools_kinds, only: kind_real,huge_real
use tools_repro, only: inf,sup
use type_mpl, only: mpl_type
@:use_probe()

implicit none

integer,parameter :: itermax = 10 !< Maximum number of iteration for the threshold definition

interface fast_fit_hor
   module procedure fit_fast_fit_hor
end interface
interface fast_fit_ver
   module procedure fit_fast_fit_ver
end interface
interface ver_smooth
   module procedure fit_ver_smooth
end interface
interface ver_fill
   module procedure fit_ver_fill
end interface

private
public :: fast_fit_hor,fast_fit_ver,ver_smooth,ver_fill

contains

!----------------------------------------------------------------------
! Subroutine: fit_fast_fit_hor
!> Fast fit length-scale estimation based on the value at mid-height (horizontal)
!----------------------------------------------------------------------
subroutine fit_fast_fit_hor(mpl,n,iz,dist,raw,fit_pk,fit_r)

implicit none

! Passed variables
type(mpl_type),intent(inout) :: mpl   !< MPI data
integer,intent(in) :: n               !< Vector size
integer,intent(in) :: iz              !< Zero separation index
real(kind_real),intent(in) :: dist(n) !< Distance
real(kind_real),intent(in) :: raw(n)  !< Raw data
real(kind_real),intent(in) :: fit_pk  !< Default peakness
real(kind_real),intent(out) :: fit_r  !< Fast fit length-scale

! Local variables
integer :: di,i,im,ip,iter
real(kind_real) :: th,thinv,dthinv,thtest
real(kind_real) :: fit_rm,fit_rp,distmin
real(kind_real) :: raw_tmp(n)

! Set name
@:set_name(fit_fast_fit_hor)

! Probe in
@:probe_in()

if (any(dist<zero)) call mpl%abort('${subr}$','negative distance')

if (raw(iz)>zero) then
   if (n>1) then
      ! Copy points that are lower than the zero-separation
      raw_tmp = mpl%msv%valr
      raw_tmp(iz) = one
      do i=1,n
        if (i/=iz) then
           if (inf(raw(i),raw(iz))) raw_tmp(i) = raw(i)/raw(iz)
        end if
      end do

      if (count(mpl%msv%isnot(raw_tmp))>1) then
         if (count(raw_tmp>zero)>1) then
            ! Curve-dependent threshold
            th = half*(one+minval(raw_tmp,mask=(raw_tmp>zero)))

            ! Find inverse threshold with a dichotomy
            thinv = half
            dthinv = quarter
            do iter=1,itermax
               thtest = fit_func(mpl,thinv,fit_pk,zero,zero,zero)
               if (sup(th,thtest)) then
                  thinv = thinv-dthinv
               else
                  thinv = thinv+dthinv
               end if
               dthinv = half*dthinv
            end do

            ! Find support radius, lower value
            fit_rm = mpl%msv%valr
            ip = iz
            do di=1,n
               ! Check whether fit value has been found
               if (mpl%msv%is(fit_rm)) then
                  ! Index
                  im = iz-di

                  ! Check index validity
                  if (im>=1) then
                     ! Check raw value validity
                     if (raw_tmp(im)>zero) then
                        ! Check whether threshold has been crossed
                        if (inf(raw_tmp(im),th)) then
                           ! Set fit value
                           fit_rm = dist(im)+(dist(ip)-dist(im))*(th-raw_tmp(im))/(raw_tmp(ip)-raw_tmp(im))
                        else
                           ! Update index
                           ip = im
                        end if
                     end if
                  end if
               end if
            end do

            ! Find support radius, upper value
            fit_rp = mpl%msv%valr
            im = iz
            do di=1,n
               ! Check whether fit value has been found
               if (mpl%msv%is(fit_rp)) then
                  ! Index
                  ip = iz+di

                  ! Check index validity
                  if (ip<=n) then
                     ! Check raw value validity
                     if (raw_tmp(ip)>zero) then
                        ! Check whether threshold has been crossed
                        if (inf(raw_tmp(ip),th)) then
                           ! Set fit value
                           fit_rp = dist(im)+(dist(ip)-dist(im))*(th-raw_tmp(im))/(raw_tmp(ip)-raw_tmp(im))
                        else
                           ! Update index
                           im = ip
                        end if
                     end if
                  end if
               end if
            end do

            ! Gather values
            if (mpl%msv%isnot(fit_rm).and.mpl%msv%isnot(fit_rp)) then
               fit_r = half*(fit_rm+fit_rp)
            elseif (mpl%msv%isnot(fit_rm)) then
               fit_r = fit_rm
            elseif (mpl%msv%isnot(fit_rp)) then
               fit_r = fit_rp
            end if

            ! Normalize
            if (mpl%msv%isnot(fit_r)) fit_r = fit_r/thinv

            ! Check positivity
            if (inf(fit_r,zero)) fit_r = mpl%msv%valr
         else
            ! All positive-separation points are negative
            fit_r = zero
         end if
      else
         ! All positive-separation points are missing
         fit_r = mpl%msv%valr
      end if

      ! Set minimum distance
      if (mpl%msv%isnot(fit_r)) then
         distmin = huge_real
         if (iz>1) distmin = min(distmin,1.0e-6_kind_real*abs(dist(iz-1)-dist(iz)))
         if (iz<n) distmin = min(distmin,1.0e-6_kind_real*abs(dist(iz+1)-dist(iz)))
         fit_r = max(fit_r,distmin)
      end if
   else
      ! Only one point, zero radius
      fit_r = zero
   end if
else
   ! Zero-separation point is negative
   fit_r = mpl%msv%valr
end if

! Probe out
@:probe_out()

end subroutine fit_fast_fit_hor

!----------------------------------------------------------------------
! Subroutine: fit_fast_fit_ver
!> Fast fit length-scale estimation based on the value at mid-height (vertical)
!----------------------------------------------------------------------
subroutine fit_fast_fit_ver(mpl,n,iz,dist,raw,fit_r,fit_nld,fit_nlr)

implicit none

! Passed variables
type(mpl_type),intent(inout) :: mpl    !< MPI data
integer,intent(in) :: n                !< Vector size
integer,intent(in) :: iz               !< Zero separation index
real(kind_real),intent(in) :: dist(n)  !< Distance
real(kind_real),intent(in) :: raw(n)   !< Raw data
real(kind_real),intent(out) :: fit_r   !< Fast fit length-scale
real(kind_real),intent(out) :: fit_nld !< Fast fit negative lobes depth
real(kind_real),intent(out) :: fit_nlr !< Fast fit negative lobes ratio

! Local variables
integer :: i,tmp(1),inlr
real(kind_real) :: raw_tmp(n),fit_minval_m,fit_minval_p,fit_minval,fit_mindist_m,fit_mindist_p,fit_mindist,ncount
real(kind_real) :: fit_r_test,fit_nld_test,fit_nlr_test,distnorm,fit(n),cost,cost_min

! Set name
@:set_name(fit_fast_fit_ver)

! Probe in
@:probe_in()

if (any(dist<zero)) call mpl%abort('${subr}$','negative distance')

if (raw(iz)>zero) then
   if (n>1) then
      ! Copy points that are lower than the zero-separation
      raw_tmp = mpl%msv%valr
      raw_tmp(iz) = one
      do i=1,n
        if (i/=iz) then
           if (inf(raw(i),raw(iz))) raw_tmp(i) = raw(i)/raw(iz)
        end if
      end do

      if (count(mpl%msv%isnot(raw_tmp))>1) then
         if (any(raw_tmp<0.05)) then
            ! Initialization
            fit_minval_m = mpl%msv%valr
            fit_minval_p = mpl%msv%valr
            fit_mindist_m = mpl%msv%valr
            fit_mindist_p = mpl%msv%valr

            ! Look for negative lobes
            if (iz>1) then
               ! Lower values
               if (any(raw_tmp(1:iz)<0.05)) then
                  fit_minval_m = minval(raw_tmp(1:iz),mask=mpl%msv%isnot(raw_tmp(1:iz)))
                  tmp = minloc(raw_tmp(1:iz),mask=mpl%msv%isnot(raw_tmp(1:iz)))
                  fit_mindist_m = abs(dist(tmp(1))-dist(iz))
               end if
            end if
            if (iz<n) then
               ! Upper values
               if (any(raw_tmp(iz:n)<0.05)) then
                  fit_minval_p = minval(raw_tmp(iz:n),mask=mpl%msv%isnot(raw_tmp(iz:n)))
                  tmp = minloc(raw_tmp(iz:n),mask=mpl%msv%isnot(raw_tmp(iz:n)))
                  fit_mindist_p = abs(dist(tmp(1))-dist(iz))
               end if
            end if

            ! Gather values
            fit_minval = zero
            fit_mindist = zero
            if (mpl%msv%isnot(fit_minval_m)) then
               fit_minval = fit_minval+fit_minval_m
               fit_mindist = fit_mindist+fit_mindist_m
               ncount = ncount+one
            end if
            if (mpl%msv%isnot(fit_minval_p)) then
               fit_minval = fit_minval+fit_minval_p
               fit_mindist = fit_mindist+fit_mindist_p
               ncount = ncount+one
            end if
            if (ncount>zero) then
               fit_minval = fit_minval/ncount
               fit_mindist = fit_mindist/ncount
            end if

            ! Find optimal nld/nlr
            cost_min = huge_real
            do inlr=2,nnlr-1
               ! Get nld/nlr
               tmp = 1+minloc(abs(fit_minval-ver_minval(2:nnld-1,inlr)))
               fit_r_test = fit_mindist/ver_minarg(tmp(1),inlr)
               fit_nld_test = real(tmp(1)-1,kind_real)*dnld
               fit_nlr_test = real(inlr-1,kind_real)*dnlr

               ! Compute cost
               do i=1,n
                  distnorm = abs(dist(i)-dist(iz))/fit_r_test
                  fit(i) = fit_func(mpl,zero,zero,distnorm,fit_nld_test,fit_nlr_test)
               end do
               cost = sum((fit-raw)**2,mask=mpl%msv%isnot(raw))

               ! Compare cost
               if (cost<cost_min) then
                  fit_r = fit_r_test
                  fit_nld = fit_nld_test
                  fit_nlr = fit_nlr_test
               end if
            end do
         else
            ! No negative lobes, use horizontal fast fit
            fit_nld = zero
            call fast_fit_hor(mpl,n,iz,dist,raw,zero,fit_r)
         end if
      else
         ! All positive-separation points are missing
         fit_r = mpl%msv%valr
         fit_nld = mpl%msv%valr
      end if
   else
      ! Only one point, zero radius
      fit_r = zero
      fit_nld = zero
   end if
else
   ! Zero-separation point is negative
   fit_r = mpl%msv%valr
   fit_nld = mpl%msv%valr
end if

! Probe out
@:probe_out()

end subroutine fit_fast_fit_ver

!----------------------------------------------------------------------
! Subroutine: fit_ver_smooth
!> Homogeneous smoothing of a vertical profile
!----------------------------------------------------------------------
subroutine fit_ver_smooth(mpl,n,x,rv,profile)

implicit none

! Passed variables
type(mpl_type),intent(inout) :: mpl         !< MPI data
integer,intent(in) :: n                     !< Vector size
real(kind_real),intent(in) :: x(n)          !< Coordinate
real(kind_real),intent(in) :: rv            !< Filtering support radius
real(kind_real),intent(inout) :: profile(n) !< Vertical profile

! Local variables
integer :: i,j
real(kind_real) :: kernel(n,n),distnorm,profile_init(n),norm

! Set name
@:set_name(fit_ver_smooth)

! Probe in
@:probe_in()

if (n>1) then
   if (rv<zero) call mpl%abort('${subr}$','negative filtering support radius')

   if ((rv>zero).and.mpl%msv%isanynot(profile)) then
      ! Vertical smoothing kernel
      kernel = zero
      do i=1,n
         do j=1,n
            if (mpl%msv%isnot(profile(i)).and.mpl%msv%isnot(profile(j))) then
               ! Gaspari-Cohn (1999) function
               distnorm = abs(x(j)-x(i))/rv
               kernel(i,j) = fit_func(mpl,distnorm,zero,zero,zero,zero)
            end if
         end do
      end do

      ! Apply kernel
      profile_init = profile
      profile = zero
      do i=1,n
         norm = zero
         do j=1,n
            profile(i) = profile(i)+kernel(i,j)*profile_init(j)
            norm = norm+kernel(i,j)
         end do
         if (norm>zero) then
            profile(i) = profile(i)/norm
         else
            profile(i) = mpl%msv%valr
         end if
      end do
   end if
end if

! Probe out
@:probe_out()

end subroutine fit_ver_smooth

!----------------------------------------------------------------------
! Subroutine: fit_ver_fill
!> Missing values filling of a vertical profile
!----------------------------------------------------------------------
subroutine fit_ver_fill(mpl,n,x,profile)

implicit none

! Passed variables
type(mpl_type),intent(inout) :: mpl         !< MPI data
integer,intent(in) :: n                     !< Vector size
real(kind_real),intent(in) :: x(n)          !< Coordinate
real(kind_real),intent(inout) :: profile(n) !< Vertical profile

! Local variables
integer :: i,j,iinf,isup
real(kind_real) :: profile_init(n)

! Set name
@:set_name(fit_ver_fill)

! Probe in
@:probe_in()

if (mpl%msv%isanynot(profile)) then
   ! Initialization
   profile_init = profile
   iinf = mpl%msv%vali

   do i=1,n
      if (mpl%msv%isnot(profile_init(i))) then
         ! Valid inferior point
         iinf = i
      else
         ! Look for a superior point
         isup = mpl%msv%vali
         j = i+1
         do while ((j<=n).and.(mpl%msv%is(isup)))
            if (mpl%msv%isnot(profile_init(j))) isup = j
            j = j+1
         end do

         if (mpl%msv%isnot(iinf).and.mpl%msv%isnot(isup)) then
            ! Interpolation
            profile(i) = profile_init(iinf)+(x(i)-x(iinf))*(profile_init(isup)-profile_init(iinf))/(x(isup)-x(iinf))
         elseif (mpl%msv%isnot(isup)) then
            ! Extrapolation with nearest superior point
            profile(i) = profile(isup)
         elseif (mpl%msv%isnot(iinf)) then
            ! Extrapolation with nearest inferior point
            profile(i) = profile(iinf)
         else
            call mpl%abort('${subr}$','too many missing values')
         end if
      end if
   end do
end if

! Probe out
@:probe_out()

end subroutine fit_ver_fill

end module tools_fit
