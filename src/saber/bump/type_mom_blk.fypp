#:include 'instrumentation.fypp'
!----------------------------------------------------------------------
! Module: type_mom_blk
!> Moments block derived type
! Author: Benjamin Menetrier
! Licensing: this code is distributed under the CeCILL-C license
! Copyright 2015-... UCAR, CERFACS, METEO-FRANCE and IRIT
!----------------------------------------------------------------------
module type_mom_blk

use tools_const, only: zero
use tools_kinds, only: kind_real
use type_geom, only: geom_type
use type_mpl, only: mpl_type
use type_nam, only: nam_type
@:use_probe()
use type_samp, only: samp_type

implicit none

! Moments block derived type
type mom_blk_type
   integer :: ne                                   !< Ensemble size
   integer :: nsub                                 !< Number of sub-ensembles
   real(kind_real),allocatable :: m1_1(:,:,:)      !< Mean for variable 1
   real(kind_real),allocatable :: m1_2(:,:,:,:,:)  !< Mean for variable 2
   real(kind_real),allocatable :: m2_1(:,:,:)      !< Variance for variable 1
   real(kind_real),allocatable :: m2_2(:,:,:,:,:)  !< Variance for variable 2
   real(kind_real),allocatable :: m11(:,:,:,:,:,:) !< Covariance
   real(kind_real),allocatable :: m12(:,:,:,:,:,:) !< Third-order centered moment 1-2
   real(kind_real),allocatable :: m21(:,:,:,:,:,:) !< Third-order centered moment 2-1
   real(kind_real),allocatable :: m22(:,:,:,:,:,:) !< Fourth-order centered moment
contains
   procedure :: alloc => mom_blk_alloc
   procedure :: init => mom_blk_init
   procedure :: dealloc => mom_blk_dealloc
   procedure :: ext => mom_blk_ext
end type mom_blk_type

private
public :: mom_blk_type

contains

!----------------------------------------------------------------------
! Subroutine: mom_blk_alloc
!> Allocation
!----------------------------------------------------------------------
subroutine mom_blk_alloc(mom_blk,nam,geom,nc1x,ne,nsub,iterative_algo)

implicit none

! Passed variables
class(mom_blk_type),intent(inout) :: mom_blk !< Moments block
type(nam_type),intent(in) :: nam             !< Namelist
type(geom_type),intent(in) :: geom           !< Geometry
integer,intent(in) :: nc1x                   !< Sampling size on subset Sc1, halo X
integer,intent(in) :: ne                     !< Ensemble size
integer,intent(in) :: nsub                   !< Number of sub-ensembles
logical,intent(in) :: iterative_algo         !< Iterative algorithm

! Set name
@:set_name(mom_blk_alloc)

! Probe in
@:probe_in()

! Attributes
mom_blk%ne = ne
mom_blk%nsub = nsub

! Allocation
if (iterative_algo) then
   allocate(mom_blk%m1_1(nc1x,geom%nl0,nsub))
   allocate(mom_blk%m1_2(nc1x,nam%nc3,nam%nc4,geom%nl0,nsub))
end if
allocate(mom_blk%m2_1(nc1x,geom%nl0,nsub))
allocate(mom_blk%m2_2(nc1x,nam%nc3,nam%nc4,geom%nl0,nsub))
allocate(mom_blk%m11(nc1x,nam%nc3,nam%nc4,nam%nl0r,geom%nl0,nsub))
if (iterative_algo) then
   allocate(mom_blk%m12(nc1x,nam%nc3,nam%nc4,nam%nl0r,geom%nl0,nsub))
   allocate(mom_blk%m21(nc1x,nam%nc3,nam%nc4,nam%nl0r,geom%nl0,nsub))
end if
allocate(mom_blk%m22(nc1x,nam%nc3,nam%nc4,nam%nl0r,geom%nl0,nsub))

! Probe out
@:probe_out()

end subroutine mom_blk_alloc

!----------------------------------------------------------------------
! Subroutine: mom_blk_init
!> Initialization
!----------------------------------------------------------------------
subroutine mom_blk_init(mom_blk,iterative_algo)

implicit none

! Passed variables
class(mom_blk_type),intent(inout) :: mom_blk !< Moments block
logical,intent(in) :: iterative_algo         !< Iterative algorithm

! Set name
@:set_name(mom_blk_init)

! Probe in
@:probe_in()

! Initialization
if (iterative_algo) then
   mom_blk%m1_1 = zero
   mom_blk%m1_2 = zero
end if
mom_blk%m2_1 = zero
mom_blk%m2_2 = zero
mom_blk%m11 = zero
if (iterative_algo) then
   mom_blk%m12 = zero
   mom_blk%m21 = zero
end if
mom_blk%m22 = zero

! Probe out
@:probe_out()

end subroutine mom_blk_init

!----------------------------------------------------------------------
! Subroutine: mom_blk_dealloc
!> Release memory
!----------------------------------------------------------------------
subroutine mom_blk_dealloc(mom_blk)

implicit none

! Passed variables
class(mom_blk_type),intent(inout) :: mom_blk !< Moments block

! Set name
@:set_name(mom_blk_dealloc)

! Probe in
@:probe_in()

! Release memory
if (allocated(mom_blk%m1_1)) deallocate(mom_blk%m1_1)
if (allocated(mom_blk%m1_2)) deallocate(mom_blk%m1_2)
if (allocated(mom_blk%m2_1)) deallocate(mom_blk%m2_1)
if (allocated(mom_blk%m2_2)) deallocate(mom_blk%m2_2)
if (allocated(mom_blk%m11)) deallocate(mom_blk%m11)
if (allocated(mom_blk%m12)) deallocate(mom_blk%m12)
if (allocated(mom_blk%m21)) deallocate(mom_blk%m21)
if (allocated(mom_blk%m22)) deallocate(mom_blk%m22)

! Probe out
@:probe_out()

end subroutine mom_blk_dealloc

!----------------------------------------------------------------------
! Subroutine: mom_blk_ext
!> Halo extension
!----------------------------------------------------------------------
subroutine mom_blk_ext(mom_blk_out,mpl,nam,geom,samp,mom_blk_in)

implicit none

! Passed variables
class(mom_blk_type),intent(inout) :: mom_blk_out !< Extended moments block
type(mpl_type),intent(inout) :: mpl              !< MPI data
type(nam_type),intent(in) :: nam                 !< Namelist
type(geom_type),intent(in) :: geom               !< Geometry
type(samp_type),intent(in) :: samp               !< Sampling
type(mom_blk_type),intent(in) :: mom_blk_in      !< Reduced moments block

! Local variables
integer :: npack,ipack,isub,il0,jc3,jc4,jl0r
real(kind_real),allocatable :: sbuf(:,:),rbuf(:,:)

! Set name
@:set_name(mom_blk_ext)

! Probe in
@:probe_in()

! Allocation
call mom_blk_out%alloc(nam,geom,samp%nc1d,mom_blk_in%ne,mom_blk_in%nsub,.false.)
npack = (1+nam%nc3*nam%nc4*(1+2*nam%nl0r))*geom%nl0*mom_blk_out%nsub
allocate(sbuf(samp%nc1a,npack))
allocate(rbuf(samp%nc1d,npack))

! Pack
ipack = 0
do isub=1,mom_blk_out%nsub
   do il0=1,geom%nl0
      ipack = ipack+1
      sbuf(:,ipack) = mom_blk_in%m2_1(:,il0,isub)
      do jc4=1,nam%nc4
         do jc3=1,nam%nc3
            ipack = ipack+1
            sbuf(:,ipack) = mom_blk_in%m2_2(:,jc3,jc4,il0,isub)
            do jl0r=1,nam%nl0r
               ipack = ipack+1
               sbuf(:,ipack) = mom_blk_in%m11(:,jc3,jc4,jl0r,il0,isub)
               ipack = ipack+1
               sbuf(:,ipack) = mom_blk_in%m22(:,jc3,jc4,jl0r,il0,isub)
            end do
         end do
      end do
   end do
end do

! Halo extension
call samp%com_c1_AD%ext(mpl,sbuf,rbuf)

! Unpack
ipack = 0
do isub=1,mom_blk_out%nsub
   do il0=1,geom%nl0
      ipack = ipack+1
      mom_blk_out%m2_1(:,il0,isub) = rbuf(:,ipack)
      do jc4=1,nam%nc4
         do jc3=1,nam%nc3
            ipack = ipack+1
            mom_blk_out%m2_2(:,jc3,jc4,il0,isub) = rbuf(:,ipack)
            do jl0r=1,nam%nl0r
               ipack = ipack+1
               mom_blk_out%m11(:,jc3,jc4,jl0r,il0,isub) = rbuf(:,ipack)
               ipack = ipack+1
               mom_blk_out%m22(:,jc3,jc4,jl0r,il0,isub) = rbuf(:,ipack)
            end do
         end do
      end do
   end do
end do

! Release memory
deallocate(sbuf)
deallocate(rbuf)

! Probe out
@:probe_out()

end subroutine mom_blk_ext

end module type_mom_blk
