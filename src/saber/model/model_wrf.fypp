#:def include_wrf()
#:include '../instrumentation.fypp'
!----------------------------------------------------------------------
! Subroutine: model_wrf_coord
!> Get WRF coordinates
!----------------------------------------------------------------------
subroutine model_wrf_coord(model,mpl,nam)

implicit none

! Passed variables
class(model_type),intent(inout) :: model !< Model
type(mpl_type),intent(inout) :: mpl      !< MPI data
type(nam_type),intent(in) :: nam         !< Namelist

! Local variables
integer :: img,ilon,ilat
integer :: ncid,lon_id,lat_id,pres_id
real(kind_real) :: dx,dy
real(kind_real),allocatable :: lon(:,:),lat(:,:),pres(:)

! Set name
@:set_name(model_wrf_coord)

! Probe in
@:probe_in()

! Open file and get dimensions
model%ntile = 1
ncid = open_file(mpl,'grid',0)
model%nlon = inquire_dim_size(mpl,ncid,'west_east')
model%nlat = inquire_dim_size(mpl,ncid,'south_north')
model%nmg = model%nlon*model%nlat
model%nlev = inquire_dim_size(mpl,ncid,'bottom_top')

! Allocation
call model%alloc
allocate(lon(model%nlon,model%nlat))
allocate(lat(model%nlon,model%nlat))
allocate(pres(model%nlev))

! Read data and close file
lon_id = inquire_var(mpl,ncid,'XLONG')
lat_id = inquire_var(mpl,ncid,'XLAT')
pres_id = inquire_var(mpl,ncid,'PB')
call get_var(mpl,ncid,lon_id,lon,(/1,1,1/),(/model%nlon,model%nlat,1/))
call get_var(mpl,ncid,lat_id,lat,(/1,1,1/),(/model%nlon,model%nlat,1/))
call get_var(mpl,ncid,pres_id,pres)
call get_att(mpl,ncid,'DX',dx)
call get_att(mpl,ncid,'DY',dy)
call close_file(mpl,ncid)

! Convert to radian
lon = lon*deg2rad
lat = lat*deg2rad

! Model grid
img = 0
do ilon=1,model%nlon
   do ilat=1,model%nlat
      img = img+1
      model%mg_to_tile(img) = 1
      model%mg_to_lon(img) = ilon
      model%mg_to_lat(img) = ilat
      model%lon(img) = lon(ilon,ilat)
      model%lat(img) = lat(ilon,ilat)
   end do
end do
model%area = dx*dy/req**2
model%mask = .true.

! Vertical unit
do img=1,model%nmg
   if (nam%logpres) then
      model%vunit(img,1:nam%nl0) = log(pres(nam%levs(1:nam%nl0)))
      if (model%nl0>nam%nl0) model%vunit(img,model%nl0) = log(ps)
   else
      model%vunit(img,:) = real(nam%levs(1:model%nl0),kind_real)
   end if
end do

! Release memory
deallocate(lon)
deallocate(lat)
deallocate(pres)

! Probe out
@:probe_out()

end subroutine model_wrf_coord

!----------------------------------------------------------------------
! Subroutine: model_wrf_read
!> Read WRF field
!----------------------------------------------------------------------
subroutine model_wrf_read(model,mpl,nam,filename,fld)

implicit none

! Passed variables
class(model_type),intent(inout) :: model                        !< Model
type(mpl_type),intent(inout) :: mpl                             !< MPI data
type(nam_type),intent(in) :: nam                                !< Namelist
character(len=*),intent(in) :: filename                         !< File name
real(kind_real),intent(out) :: fld(model%nmga,model%nl0,nam%nv) !< Field

! Local variables
integer :: iv,il0,img,ilon,ilat,rank
integer :: ncid,fld_id
real(kind_real) :: fld_tmp2,fld_mg(model%nmg,model%nl0)
real(kind_real),allocatable :: fld_tmp(:,:,:)

! Set name
@:set_name(model_wrf_read)

! Probe in
@:probe_in()

if (mpl%main) then
   ! Allocation
   allocate(fld_tmp(model%nlon,model%nlat,model%nl0))

   ! Open file
   ncid = open_file(mpl,filename,0)
end if

do iv=1,nam%nv
   if (mpl%main) then
      ! Get variable ID
      fld_id = inquire_var(mpl,ncid,nam%variables(iv))

      ! Get variable rank
      rank = inquire_var_rank(mpl,ncid,nam%variables(iv))

      ! Read data
      select case (rank)
      case (3)
         ! 2D data
         fld_tmp = mpl%msv%valr
         if (trim(nam%lev2d)=='first') then
            call get_var(mpl,ncid,fld_id,fld_tmp(:,:,1),(/1,1,1/),(/model%nlon,model%nlat,1/))
         elseif (trim(nam%lev2d)=='last') then
            call get_var(mpl,ncid,fld_id,fld_tmp(:,:,model%nl0),(/1,1,1/),(/model%nlon,model%nlat,1/))
         end if
      case (4)
         ! 3D data
         do il0=1,nam%nl0
            call get_var(mpl,ncid,fld_id,fld_tmp(:,:,il0),(/1,1,nam%levs(il0),1/),(/model%nlon,model%nlat,1,1/))
            select case (trim(nam%variables(iv)))
            case ('U')
               do ilat=1,model%nlat
                  do ilon=1,model%nlon
                     call get_var(mpl,ncid,fld_id,fld_tmp2,(/ilon+1,ilat,nam%levs(il0),1/))
                     fld_tmp(ilon,ilat,il0) = half*(fld_tmp(ilon,ilat,il0)+fld_tmp2)
                  end do
               end do
            case ('V')
               do ilat=1,model%nlat
                  do ilon=1,model%nlon
                     call get_var(mpl,ncid,fld_id,fld_tmp2,(/ilon,ilat+1,nam%levs(il0),1/))
                     fld_tmp(ilon,ilat,il0) = half*(fld_tmp(ilon,ilat,il0)+fld_tmp2)
                  end do
               end do
            end select
         end do
      case default
         call mpl%abort('${subr}$','wrong number of dimensions for variable '//trim(nam%variables(iv)))
      end select


      ! Read data
      do il0=1,nam%nl0
         call get_var(mpl,ncid,fld_id,fld_tmp(:,:,il0),(/1,1,nam%levs(il0),1/),(/model%nlon,model%nlat,1,1/))
         select case (trim(nam%variables(iv)))
         case ('U')
            do ilat=1,model%nlat
               do ilon=1,model%nlon
                  call get_var(mpl,ncid,fld_id,fld_tmp2,(/ilon+1,ilat,nam%levs(il0),1/))
                  fld_tmp(ilon,ilat,il0) = half*(fld_tmp(ilon,ilat,il0)+fld_tmp2)
               end do
            end do
         case ('V')
            do ilat=1,model%nlat
               do ilon=1,model%nlon
                  call get_var(mpl,ncid,fld_id,fld_tmp2,(/ilon,ilat+1,nam%levs(il0),1/))
                  fld_tmp(ilon,ilat,il0) = half*(fld_tmp(ilon,ilat,il0)+fld_tmp2)
               end do
            end do
         end select
      end do

      ! Pack
      do il0=1,model%nl0
         do img=1,model%nmg
            ilon = model%mg_to_lon(img)
            ilat = model%mg_to_lat(img)
            fld_mg(img,il0) = fld_tmp(ilon,ilat,il0)
         end do
      end do
   end if
   call mpl%glb_to_loc(model%nmga,model%nmg,model%mga_to_mg,fld_mg,fld(:,:,iv))
end do

if (mpl%main) then
   ! Close file
   call close_file(mpl,ncid)

   ! Release memory
   deallocate(fld_tmp)
end if

! Probe out
@:probe_out()

end subroutine model_wrf_read
#:enddef include_wrf
