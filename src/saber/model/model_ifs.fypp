#:def include_ifs()
#:include '../instrumentation.fypp'
!----------------------------------------------------------------------
! Subroutine: model_ifs_coord
!> Get IFS coordinates
!----------------------------------------------------------------------
subroutine model_ifs_coord(model,mpl,nam)

implicit none

! Passed variables
class(model_type),intent(inout) :: model !< Model
type(mpl_type),intent(inout) :: mpl      !< MPI data
type(nam_type),intent(in) :: nam         !< Namelist

! Local variables
integer :: img,ilon,ilat
integer :: ncid,lon_id,lat_id,pres_id
real(kind_real),allocatable :: lon(:),lat(:),pres(:)

! Set name
@:set_name(model_ifs_coord)

! Probe in
@:probe_in()

! Open file and get dimensions
model%ntile = 1
ncid = open_file(mpl,'grid',0)
model%nlon = inquire_dim_size(mpl,ncid,'longitude')
model%nlat = inquire_dim_size(mpl,ncid,'latitude')
model%nmg = model%nlon*model%nlat
model%nlev = inquire_dim_size(mpl,ncid,'level')

! Allocation
call model%alloc
allocate(lon(model%nlon))
allocate(lat(model%nlat))
allocate(pres(model%nlev))

! Read data and close file
lon_id = inquire_var(mpl,ncid,'longitude')
lat_id = inquire_var(mpl,ncid,'latitude')
pres_id = inquire_var(mpl,ncid,'pf')
call get_var(mpl,ncid,lon_id,lon)
call get_var(mpl,ncid,lat_id,lat)
call get_var(mpl,ncid,pres_id,pres)
call close_file(mpl,ncid)

! Convert to radian
lon = lon*deg2rad
lat = lat*deg2rad

! Model grid
img = 0
do ilon=1,model%nlon
   do ilat=1,model%nlat
      img = img+1
      model%mg_to_tile(img) = 1
      model%mg_to_lon(img) = ilon
      model%mg_to_lat(img) = ilat
      model%lon(img) = lon(ilon)
      model%lat(img) = lat(ilat)
   end do
end do
model%area = four*pi/real(model%nmg,kind_real)
model%mask = .true.

! Vertical unit
do img=1,model%nmg
   if (nam%logpres) then
      model%vunit(img,1:nam%nl0) = log(pres(nam%levs(1:nam%nl0)))
      if (model%nl0>nam%nl0) model%vunit(img,model%nl0) = log(ps)
   else
      model%vunit(img,:) = real(nam%levs(1:model%nl0),kind_real)
   end if
end do
! Release memory
deallocate(lon)
deallocate(lat)

! Probe out
@:probe_out()

end subroutine model_ifs_coord

!----------------------------------------------------------------------
! Subroutine: model_ifs_read
!> Read IFS field
!----------------------------------------------------------------------
subroutine model_ifs_read(model,mpl,nam,filename,fld)

implicit none

! Passed variables
class(model_type),intent(inout) :: model                        !< Model
type(mpl_type),intent(inout) :: mpl                             !< MPI data
type(nam_type),intent(in) :: nam                                !< Namelist
character(len=*),intent(in) :: filename                         !< File name
real(kind_real),intent(out) :: fld(model%nmga,model%nl0,nam%nv) !< Field

! Local variables
integer :: iv,il0,img,ilon,ilat
integer :: ncid,fld_id
real(kind_real) :: fld_mg(model%nmg,model%nl0)
real(kind_real),allocatable :: fld_tmp(:,:,:)

! Set name
@:set_name(model_ifs_read)

! Probe in
@:probe_in()

if (mpl%main) then
   ! Allocation
   allocate(fld_tmp(model%nlon,model%nlat,model%nl0))

   ! Open file
   ncid = open_file(mpl,filename,0)
end if

do iv=1,nam%nv
   if (mpl%main) then
      ! Get variable id
      fld_id = inquire_var(mpl,ncid,nam%variables(iv))

      ! Read data
      do il0=1,nam%nl0
         call get_var(mpl,ncid,fld_id,fld_tmp(:,:,il0),(/1,1,nam%levs(il0),1/),(/model%nlon,model%nlat,1,1/))
      end do

      ! Pack
      do il0=1,model%nl0
         do img=1,model%nmg
            ilon = model%mg_to_lon(img)
            ilat = model%mg_to_lat(img)
            fld_mg(img,il0) = fld_tmp(ilon,ilat,il0)
         end do
      end do
   end if
   call mpl%glb_to_loc(model%nmga,model%nmg,model%mga_to_mg,fld_mg,fld(:,:,iv))
end do

if (mpl%main) then
   ! Close file
   call close_file(mpl,ncid)

   ! Release memory
   deallocate(fld_tmp)
end if

! Probe out
@:probe_out()

end subroutine model_ifs_read
#:enddef include_ifs
