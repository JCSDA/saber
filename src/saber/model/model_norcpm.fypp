#:def include_norcpm()
#:include '../instrumentation.fypp'
!----------------------------------------------------------------------
! Subroutine: model_norcpm_coord
!> Get NorCPM coordinates
!----------------------------------------------------------------------
subroutine model_norcpm_coord(model,mpl,nam)

implicit none

! Passed variables
class(model_type),intent(inout) :: model !< Model
type(mpl_type),intent(inout) :: mpl      !< MPI data
type(nam_type),intent(in) :: nam         !< Namelist

! Local variables
integer :: img,il0,ilon,ilat
integer :: ncid,lon_id,lat_id,area_id,gmask_id,dp_id
integer,allocatable :: gmask(:,:)
real(kind_double),allocatable :: lon(:,:),lat(:,:),area(:,:),dp(:,:,:)

! Set name
@:set_name(model_norcpm_coord)

! Probe in
@:probe_in()

! Open file and get dimensions
model%ntile = 1
ncid = open_file(mpl,'grid',0)
model%nlon = inquire_dim_size(mpl,ncid,'x')
model%nlat = inquire_dim_size(mpl,ncid,'y')
model%nmg = model%nlon*model%nlat
model%nlev = inquire_dim_size(mpl,ncid,'kk2')
model%nlev = model%nlev/2

! Allocation
call model%alloc
allocate(lon(model%nlon,model%nlat))
allocate(lat(model%nlon,model%nlat))
allocate(area(model%nlon,model%nlat))
allocate(gmask(model%nlon,model%nlat))
allocate(dp(model%nlon,model%nlat,model%nlev))

! Read data and close file
lon_id = inquire_var(mpl,ncid,'plon')
lat_id = inquire_var(mpl,ncid,'plat')
area_id = inquire_var(mpl,ncid,'parea')
gmask_id = inquire_var(mpl,ncid,'pmask')
dp_id = inquire_var(mpl,ncid,'dp')
call get_var(mpl,ncid,lon_id,lon)
call get_var(mpl,ncid,lat_id,lat)
call get_var(mpl,ncid,area_id,area)
call get_var(mpl,ncid,gmask_id,gmask)
call get_var(mpl,ncid,dp_id,dp,(/1,1,1/),(/1,1,model%nlev/))
call close_file(mpl,ncid)

! Model grid
img = 0
do ilon=1,model%nlon
   do ilat=1,model%nlat
      img = img+1
      model%mg_to_tile(img) = 1
      model%mg_to_lon(img) = ilon
      model%mg_to_lat(img) = ilat
      model%lon(img) = real(lon(ilon,ilat),kind_real)*deg2rad
      model%lat(img) = real(lat(ilon,ilat),kind_real)*deg2rad
      model%area(img) = real(area(ilon,ilat),kind_real)/req**2
      model%mask(img,:) = (gmask(ilon,ilat)>0)
   end do
end do

! Vertical unit
do img=1,model%nmg
   ilon = model%mg_to_lon(img)
   ilat = model%mg_to_lat(img)

   do il0=1,nam%nl0
      if (nam%levs(il0)==1) then
         model%vunit(img,il0) = ps+half*dp(ilon,ilat,1)
      else
         model%vunit(img,il0) = ps+sum(dp(ilon,ilat,1:nam%levs(il0)-1))+half*dp(ilon,ilat,nam%levs(il0))
      end if
   end do
end do

! Release memory
deallocate(lon)
deallocate(lat)
deallocate(area)
deallocate(gmask)
deallocate(dp)

! Probe out
@:probe_out()

end subroutine model_norcpm_coord

!----------------------------------------------------------------------
! Subroutine: model_norcpm_read
!> Read NorCPM field
!----------------------------------------------------------------------
subroutine model_norcpm_read(model,mpl,nam,filename,fld)

implicit none

! Passed variables
class(model_type),intent(inout) :: model                        !< Model
type(mpl_type),intent(inout) :: mpl                             !< MPI data
type(nam_type),intent(in) :: nam                                !< Namelist
character(len=*),intent(in) :: filename                         !< File name
real(kind_real),intent(out) :: fld(model%nmga,model%nl0,nam%nv) !< Field

! Local variables
integer :: iv,il0,img,ilon,ilat,rank
integer :: ncid,fld_id
real(kind_real) :: fld_mg(model%nmg,model%nl0)
real(kind_real),allocatable :: fld_tmp(:,:,:)

! Set name
@:set_name(model_norcpm_read)

! Probe in
@:probe_in()

if (mpl%main) then
   ! Allocation
   allocate(fld_tmp(model%nlon,model%nlat,model%nl0))

   ! Open file
   ncid = open_file(mpl,filename,0)
end if

do iv=1,nam%nv
   if (mpl%main) then
      ! Get variable ID
      fld_id = inquire_var(mpl,ncid,nam%variables(iv))

      ! Get variable rank
      rank = inquire_var_rank(mpl,ncid,nam%variables(iv))

      ! Read data
      select case (rank)
      case (3)
         ! 2D data
         fld_tmp = mpl%msv%valr
         if (trim(nam%lev2d)=='first') then
            call get_var(mpl,ncid,fld_id,fld_tmp(:,:,1),(/1,1,1/),(/model%nlon,model%nlat,1/))
         elseif (trim(nam%lev2d)=='last') then
            call get_var(mpl,ncid,fld_id,fld_tmp(:,:,model%nl0),(/1,1,1/),(/model%nlon,model%nlat,1/))
         end if
      case (4)
         ! 3D data
         do il0=1,nam%nl0
            call get_var(mpl,ncid,fld_id,fld_tmp(:,:,il0),(/1,1,nam%levs(il0),1/),(/model%nlon,model%nlat,1,1/))
         end do
      case default
         call mpl%abort('${subr}$','wrong number of dimensions for variable '//trim(nam%variables(iv)))
      end select

      ! Pack
      do il0=1,model%nl0
         do img=1,model%nmg
            ilon = model%mg_to_lon(img)
            ilat = model%mg_to_lat(img)
            fld_mg(img,il0) = fld_tmp(ilon,ilat,il0)
         end do
      end do
   end if
   call mpl%glb_to_loc(model%nmga,model%nmg,model%mga_to_mg,fld_mg,fld(:,:,iv))
end do

if (mpl%main) then
   ! Close file
   call close_file(mpl,ncid)

   ! Release memory
   deallocate(fld_tmp)
end if

! Probe out
@:probe_out()

end subroutine model_norcpm_read
#:enddef include_norcpm
