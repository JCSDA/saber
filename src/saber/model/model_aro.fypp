#:def include_aro()
#:include '../instrumentation.fypp'
!----------------------------------------------------------------------
! Subroutine: model_aro_coord
!> Load AROME coordinates
!----------------------------------------------------------------------
subroutine model_aro_coord(model,mpl,nam)

implicit none

! Passed variables
class(model_type),intent(inout) :: model !< Model
type(mpl_type),intent(inout) :: mpl      !< MPI data
type(nam_type),intent(in) :: nam         !< Namelist

! Local variables
integer :: img,ilon,ilat,il0,il0_bot,il0_top,ie
integer :: ncid,lon_id,lat_id,cmask_id,a_id,b_id,pp_id,fld_id,mask_id
integer,allocatable :: mask_counter(:)
real(kind_real) :: dx,dy
real(kind_real),allocatable :: lon(:,:),lat(:,:),cmask(:,:),a(:),b(:),fld_loc(:,:),fld(:,:)
character(len=3) :: ilchar
character(len=1024) :: filename

! Set name
@:set_name(model_aro_coord)

! Probe in
@:probe_in()

! Open file and get dimensions
model%ntile = 1
ncid = open_file(mpl,'grid',0)
model%nlon = inquire_dim_size(mpl,ncid,'X')
model%nlat = inquire_dim_size(mpl,ncid,'Y')
model%nmg = model%nlon*model%nlat
model%nlev = inquire_dim_size(mpl,ncid,'Z')

! Allocation
call model%alloc
allocate(lon(model%nlon,model%nlat))
allocate(lat(model%nlon,model%nlat))
allocate(cmask(model%nlon,model%nlat))
allocate(a(model%nlev+1))
allocate(b(model%nlev+1))

! Read data and close file
lon_id = inquire_var(mpl,ncid,'longitude')
lat_id = inquire_var(mpl,ncid,'latitude')
cmask_id = inquire_var(mpl,ncid,'cmask')
a_id = inquire_var(mpl,ncid,'hybrid_coef_A')
b_id = inquire_var(mpl,ncid,'hybrid_coef_B')
pp_id = inquire_var(mpl,ncid,'Projection_parameters')
call get_var(mpl,ncid,lon_id,lon)
call get_var(mpl,ncid,lat_id,lat)
call get_var(mpl,ncid,cmask_id,cmask)
call get_var(mpl,ncid,a_id,a)
call get_var(mpl,ncid,b_id,b)
call get_att(mpl,ncid,'x_resolution',dx,pp_id)
call get_att(mpl,ncid,'y_resolution',dy,pp_id)
call close_file(mpl,ncid)

! Convert to radian
lon = lon*deg2rad
lat = lat*deg2rad

! Model grid
img = 0
do ilon=1,model%nlon
   do ilat=1,model%nlat
      img = img+1
      model%mg_to_tile(img) = 1
      model%mg_to_lon(img) = ilon
      model%mg_to_lat(img) = ilat
      model%lon(img) = lon(ilon,ilat)
      model%lat(img) = lat(ilon,ilat)
      select case (trim(zone))
      case ('C')
         model%mask(img,:) = (cmask(ilon,ilat)>one-quarter)
      case ('C+I')
         model%mask(img,:) = (cmask(ilon,ilat)>quarter)
      case ('C+I+E')
         model%mask(img,:) = .true.
      case default
         call mpl%abort('${subr}$','wrong AROME zone')
      end select
   end do
end do
model%area = dx*dy/req**2

! Specific mask
select case (trim(nam%mask_type))
case ("hyd")
   ! Based on an external file

   ! Allocation
   allocate(fld_loc(model%nlon,model%nlat))

   ! Open file
   ncid = open_file(mpl,trim(nam%prefix)//'_hyd',0)

   ! Read mask
   do il0=1,nam%nl
      ! Get id
      write(ilchar,'(i3.3)') nam%levs(il0)
      mask_id = inquire_var(mpl,ncid,'S'//ilchar//'MASK')

      ! Read data
      call get_var(mpl,ncid,mask_id,fld_loc)

      ! Pack data
      img = 0
      do ilon=1,model%nlon
         do ilat=1,model%nlat
            img = img+1
            model%mask(img,il0) = (fld_loc(ilon,ilat)>nam%mask_th(1))
         end do
      end do
   end do

   ! Close file
   call close_file(mpl,ncid)

   ! Release memory
   deallocate(fld_loc)
case ("lwc_fog","lwc_clear","lwc_stratus")
   ! Based on the ensemble-mean LWC

   ! Allocation
   allocate(fld_loc(model%nlon,model%nlat))
   allocate(fld(model%nmg,model%nl0))
   allocate(mask_counter(model%nmga))

   ! Initialization
   mask_counter = 0
   select case (trim(nam%mask_type))
   case("lwc_fog")
      il0_bot = nam%nl
      il0_top = nam%nl-2
   case("lwc_clear")
      il0_bot = nam%nl
      il0_top = 1
   case("lwc_stratus")
      il0_bot = nam%nl-30
      il0_top = nam%nl-66
   end select

   ! Setup mask
   do ie=1,nam%ens1_ne
      ! Set file name
      write(filename,'(a,i6.6)') 'ens1_',ie

      ! Open file
      ncid = open_file(mpl,filename,0)

      ! Read LWC
      do il0=1,nam%nl
         ! Get id
         write(ilchar,'(i3.3)') nam%levs(il0)
         fld_id = inquire_var(mpl,ncid,'S'//ilchar//'CLOUD_WATER')

         ! Read data
         call get_var(mpl,ncid,fld_id,fld_loc)

         ! Pack data
         img = 0
         do ilon=1,model%nlon
            do ilat=1,model%nlat
               img = img+1
               fld(img,il0) = real(fld_loc(ilon,ilat),kind_real)
            end do
         end do
      end do

      ! Close file
      call close_file(mpl,ncid)

      ! Update mask
      select case (trim(nam%mask_type))
      case("lwc_fog")
         do img=1,model%nmg
            if (all(fld(img,il0_top:il0_bot)>nam%mask_th)) mask_counter(img) = mask_counter(img)+1
         end do
      case("lwc_clear")
         do img=1,model%nmg
            if (all(fld(img,il0_top:il0_bot)<nam%mask_th)) mask_counter(img) = mask_counter(img)+1
         end do
      case("lwc_stratus")
         do img=1,model%nmg
            if (count(fld(img,il0_top:il0_bot)>nam%mask_th)>3) mask_counter(img) = mask_counter(img)+1
         end do
      end select
   end do

   ! Update mask
   do img=1,model%nmg
      if (real(mask_counter(img),kind_real)<0.9_kind_real*real(nam%ens1_ne,kind_real)) model%mask(img,:) = .false.
   end do
end select

! Vertical unit
do img=1,model%nmg
   if (nam%logpres) then
      model%vunit(img,1:nam%nl) = log(half*(a(nam%levs(1:nam%nl))+a(nam%levs(1:nam%nl)+1)) &
 & +half*(b(nam%levs(1:nam%nl))+b(nam%levs(1:nam%nl)+1))*ps)
      if (model%nl0>nam%nl) model%vunit(img,model%nl0) = log(ps)
   else
      model%vunit(img,:) = real(nam%levs(1:model%nl0),kind_real)
   end if
end do

! Release memory
deallocate(lon)
deallocate(lat)
deallocate(cmask)
deallocate(a)
deallocate(b)

! Probe out
@:probe_out()

end subroutine model_aro_coord

!----------------------------------------------------------------------
! Subroutine: model_aro_read
!> Read AROME field
!----------------------------------------------------------------------
subroutine model_aro_read(model,mpl,nam,filename,fld)

implicit none

! Passed variables
class(model_type),intent(inout) :: model                        !< Model
type(mpl_type),intent(inout) :: mpl                             !< MPI data
type(nam_type),intent(in) :: nam                                !< Namelist
character(len=*),intent(in) :: filename                         !< File name
real(kind_real),intent(out) :: fld(model%nmga,model%nl0,nam%nv) !< Field

! Local variables
integer :: iv,il0,img,ilon,ilat,iv_q,iv_lwc
integer :: ncid,fld_id
real(kind_real) :: fld_mg(model%nmg,model%nl0)
real(kind_real),allocatable :: fld_tmp(:,:,:)
character(len=3) :: ilchar

! Set name
@:set_name(model_aro_read)

! Probe in
@:probe_in()

if (mpl%main) then
   ! Allocation
   allocate(fld_tmp(model%nlon,model%nlat,model%nl0))

   ! Open file
   ncid = open_file(mpl,filename,0)
end if

do iv=1,nam%nv
   if (mpl%main) then
      do il0=1,nam%nl
         ! Get id
         write(ilchar,'(i3.3)') nam%levs(il0)
         fld_id = inquire_var(mpl,ncid,'S'//ilchar//trim(nam%variables(iv)))

         ! Read data
         if (mpl%msv%isnot(fld_id)) then
            ! 3D data
            call get_var(mpl,ncid,fld_id,fld_tmp(:,:,il0))
         else
            ! Check if the variable exists as 2D data
            fld_id = inquire_var(mpl,ncid,nam%variables(iv))

            ! 2D data
            if (((trim(nam%lev2d)=='first').and.(il0==1)).or.((trim(nam%lev2d)=='last').and.(il0==model%nl0))) then
               fld_tmp = mpl%msv%valr
               call get_var(mpl,ncid,fld_id,fld_tmp(:,:,il0))
            end if
         end if
      end do

      ! Pack
      do il0=1,model%nl0
         do img=1,model%nmg
            ilon = model%mg_to_lon(img)
            ilat = model%mg_to_lat(img)
            fld_mg(img,il0) = fld_tmp(ilon,ilat,il0)
         end do
      end do
   end if
   call mpl%glb_to_loc(model%nmga,model%nmg,model%mga_to_mg,fld_mg,fld(:,:,iv))
end do

if (mpl%main) then
   ! Close file
   call close_file(mpl,ncid)

   ! Release memory
   deallocate(fld_tmp)
end if

! Post-processing
iv_q = mpl%msv%vali
iv_lwc = mpl%msv%vali
do iv=1,nam%nv
   if (trim(nam%variables(iv))=='HUMI_SPECIFI') iv_q = iv
   if (trim(nam%variables(iv))=='CLOUD_WATER') iv_lwc = iv
end do
select case (trim(nam%variable_change))
case ('logq')
   if (mpl%msv%isnot(iv_q)) then
       fld(:,:,iv_q) = log(max(fld(:,:,iv_q),qmin))
   else
      call mpl%abort('${subr}$','specific humidity not found')
   end if
case ('logqtot')
   if ((mpl%msv%isnot(iv_q)).and.(mpl%msv%isnot(iv_lwc))) then
      fld(:,:,iv_q) = log(max(fld(:,:,iv_q)+fld(:,:,iv_lwc),qmin))
      fld(:,:,iv_lwc) = mpl%msv%valr
   else
      call mpl%abort('${subr}$','specific humidity, or LWC not found')
   end if
end select

! Probe out
@:probe_out()

end subroutine model_aro_read
#:enddef include_aro
