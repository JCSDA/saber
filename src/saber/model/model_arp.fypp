#:def include_arp()
#:include '../instrumentation.fypp'
!----------------------------------------------------------------------
! Subroutine: model_arp_coord
!> Get ARPEGE coordinates
!----------------------------------------------------------------------
subroutine model_arp_coord(model,mpl,nam)

implicit none

! Passed variables
class(model_type),intent(inout) :: model !< Model
type(mpl_type),intent(inout) :: mpl      !< MPI data
type(nam_type),intent(in) :: nam         !< Namelist

! Local variables
integer :: img,ilon,ilat
integer :: ncid,lon_id,lat_id,a_id,b_id
real(kind_real),allocatable :: lon(:,:),lat(:,:),a(:),b(:)

! Set name
@:set_name(model_arp_coord)

! Probe in
@:probe_in()

! Open file and get dimensions
model%ntile = 1
ncid = open_file(mpl,'grid',0)
model%nlon = inquire_dim_size(mpl,ncid,'longitude')
model%nlat = inquire_dim_size(mpl,ncid,'latitude')
model%nlev = inquire_dim_size(mpl,ncid,'Z')

! Allocation
call model%alloc
allocate(lon(model%nlon,model%nlat))
allocate(lat(model%nlon,model%nlat))
allocate(a(model%nlev+1))
allocate(b(model%nlev+1))

! Read data and close file
lon_id = inquire_var(mpl,ncid,'longitude')
lat_id = inquire_var(mpl,ncid,'latitude')
a_id = inquire_var(mpl,ncid,'hybrid_coef_A')
b_id = inquire_var(mpl,ncid,'hybrid_coef_B')
call get_var(mpl,ncid,lon_id,lon)
call get_var(mpl,ncid,lat_id,lat)
call get_var(mpl,ncid,a_id,a)
call get_var(mpl,ncid,b_id,b)
call close_file(mpl,ncid)

! Grid size
model%nmg = count(lon>-thousand)

! Convert to radian
lon = lon*deg2rad
lat = lat*deg2rad

! Model grid
img = 0
do ilon=1,model%nlon
   do ilat=1,model%nlat
      if (lon(ilon,ilat)>-thousand) then
         img = img+1
         model%mg_to_tile(img) = 1
         model%mg_to_lon(img) = ilon
         model%mg_to_lat(img) = ilat
         model%lon(img) = lon(ilon,ilat)
         model%lat(img) = lat(ilon,ilat)
      end if
   end do
end do
model%area = four*pi/real(model%nmg,kind_real)
model%mask = .true.

! Vertical unit
do img=1,model%nmg
   if (nam%logpres) then
      model%vunit(img,1:nam%nl) = log(half*(a(nam%levs(1:nam%nl))+a(nam%levs(1:nam%nl)+1)) &
 & +half*(b(nam%levs(1:nam%nl))+b(nam%levs(1:nam%nl)+1))*ps)
      if (model%nl0>nam%nl) model%vunit(img,model%nl0) = log(ps)
   else
      model%vunit(img,:) = real(nam%levs(1:model%nl0))
   end if
end do

! Release memory
deallocate(lon)
deallocate(lat)
deallocate(a)
deallocate(b)

! Probe out
@:probe_out()

end subroutine model_arp_coord

!----------------------------------------------------------------------
! Subroutine: model_arp_read
!> Read ARPEGE field
!----------------------------------------------------------------------
subroutine model_arp_read(model,mpl,nam,filename,fld)

implicit none

! Passed variables
class(model_type),intent(inout) :: model                        !< Model
type(mpl_type),intent(inout) :: mpl                             !< MPI data
type(nam_type),intent(in) :: nam                                !< Namelist
character(len=*),intent(in) :: filename                         !< File name
real(kind_real),intent(out) :: fld(model%nmga,model%nl0,nam%nv) !< Field

! Local variables
integer :: iv,il0,img,ilon,ilat
integer :: ncid,fld_id
real(kind_real) :: fld_mg(model%nmg,model%nl0)
real(kind_real),allocatable :: fld_tmp(:,:,:)
character(len=3) :: ilchar

! Set name
@:set_name(model_arp_read)

! Probe in
@:probe_in()

if (mpl%main) then
   ! Allocation
   allocate(fld_tmp(model%nlon,model%nlat,model%nl0))

   ! Open file
   ncid = open_file(mpl,filename,0)
end if

do iv=1,nam%nv
   if (mpl%main) then
      do il0=1,nam%nl
         ! Get id
         write(ilchar,'(i3.3)') nam%levs(il0)
         fld_id = inquire_var(mpl,ncid,'S'//ilchar//trim(nam%variables(iv)))

         ! Read data
         if (mpl%msv%isnot(fld_id)) then
            ! 3D data
            call get_var(mpl,ncid,fld_id,fld_tmp(:,:,il0))
         else
            ! Check if the variable exists as 2D data
            fld_id = inquire_var(mpl,ncid,nam%variables(iv))

            ! 2D data
            if (((trim(nam%lev2d)=='first').and.(il0==1)).or.((trim(nam%lev2d)=='last').and.(il0==model%nl0))) then
               fld_tmp = mpl%msv%valr
               call get_var(mpl,ncid,fld_id,fld_tmp(:,:,il0))
            end if
         end if
      end do

      ! Pack
      do il0=1,model%nl0
         do img=1,model%nmg
            ilon = model%mg_to_lon(img)
            ilat = model%mg_to_lat(img)
            fld_mg(img,il0) = fld_tmp(ilon,ilat,il0)
         end do
      end do
   end if
   call mpl%glb_to_loc(model%nmga,model%nmg,model%mga_to_mg,fld_mg,fld(:,:,iv))
end do

if (mpl%main) then
   ! Close file
   call close_file(mpl,ncid)

   ! Release memory
   deallocate(fld_tmp)
end if

! Probe out
@:probe_out()

end subroutine model_arp_read
#:enddef include_arp
